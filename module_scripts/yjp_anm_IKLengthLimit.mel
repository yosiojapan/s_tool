/*
IKコントローラがジョイントより大きく離れた状態をジョイントに近づけます。
$softPでルートジョイントに近づけることもできます。

IK controller is moved closer to the joint with a greater distance away from the joint.
You can also use $softP to bring it closer to the root joint.
*/

global proc yjp_anm_IKLengthLimit(string $node,float $softP)
{
	print ("yjp_anm_IKLengthLimit start"+ "\n") ;
	string $metanode = `yjp_rig_getMeta $node`;
	string $joint[],$IKCTRL[];
	if(size($metanode)){
		$joint =`listConnections ($metanode + ".source")`;
		$IKCTRL = `listConnections ($metanode + ".IKctrl")`;
	}
	else{
		$IKCTRL[0] = $node;
	}
	string $jointA = $joint[0];
	
	string $jointB[] = `listRelatives -p $jointA`;
	print ($jointB[0]+ "\n") ;
	string $jointC[] = `listRelatives -p $jointB[0]`;
	print ($jointC[0]+ "\n") ;
	vector $traA = `xform -q -a -ws -t $jointA `;
	vector $traB = `xform -q -a -ws -t $jointB[0] `;
	vector $traC = `xform -q -a -ws -t $jointC[0] `;
	vector $tractrl = `xform -q -a -ws -t $IKCTRL `;
	vector $uval = unit($tractrl - $traC);
	
	float $lengthA = mag($traA - $traB);
	float $lengthB = mag($traB - $traC);
	float $lengthctrl = mag($tractrl - $traC);
	
	float $maxleng;

	if(`attributeExists "length" $metanode`){
		print ( "length "+ "\n") ;
		$maxleng = `getAttr ($metanode+".length")`;
	}
	else{
		$maxleng = $lengthA + $lengthB;
	}
	print ($maxleng + "\n") ;
	//softIKの式
	
	float $ctrlDist = $lengthctrl;
	float $softDist = $ctrlDist;
	//float $softP = 1.0;
	float $chainLen = $maxleng;
	
	if($ctrlDist > ($chainLen - $softP)){
		if($softP > 0){
			$softDist = $chainLen - $softP * exp(-($ctrlDist - ($chainLen - $softP))/$softP);
		}
		else{
			$softDist = $chainLen;
		}
	}
	
	float $val[] = $traC + ($uval * $softDist);
	move -a -ws  $val[0] $val[1] $val[2] $IKCTRL[0];
	move -a -ws  $val[0] $val[1] $val[2] $IKCTRL[0];
	if( `getAttr -k ($IKCTRL[0] +".tx")`||`getAttr -channelBox ($IKCTRL[0] +".tx")` )setKeyframe ($IKCTRL[0] +".tx");
	if( `getAttr -k ($IKCTRL[0] +".ty")`||`getAttr -channelBox ($IKCTRL[0] +".ty")` )setKeyframe ($IKCTRL[0] +".ty");
	if( `getAttr -k ($IKCTRL[0] +".tz")`||`getAttr -channelBox ($IKCTRL[0] +".tz")` )setKeyframe ($IKCTRL[0] +".tz");
	print ("yjp_anm_IKLengthLimit end"+ "\n") ;
}

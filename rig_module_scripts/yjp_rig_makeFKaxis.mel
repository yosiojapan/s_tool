/*
	SYNOPSIS
		FKコントローラーを作成
	INPUTS
		string $UtilityName
		string $FKjoint[]
		int $color
	
	RETURNS
		joint_ctrl
		
	proc
		RigCurveCircle
		RigCurveBox
		//RigCtrlCharacterSet
		resetAttrs
		yjp_rig_makeMetaNode
		yjp_rig_connectMeta
		
 */
global proc string[] yjp_rig_makeFKaxis(string $FKjoint[], float $pos[], float $rot[], int $color, int $symmetry)
{
	//int $color = 6 ;
	print ("yjp_rig_makeFKaxis\n") ;
	int $nn;
	int $p=1;
	global string $ss_RigCharaID ;
	global int $RIGversion = 0;
	global int $yjp_RiGctrlColor[];
	$yjp_RiGctrlColor[1] = $color; 
	string $ID = $ss_RigCharaID;
	string $joint_ctrl[],$NextJoint[];
	string $rootnode ;
	float $Width = 1.0;
	float $Length = 1.0;
	string $metaNode;
	string $VerList[];
	string $sclist[];
	int $n ;
	for ($n=0; $n<size($FKjoint);$n++){
		print("FKjoint " +$FKjoint[$n] + "\n");
	}

	if($ss_RigCharaID == ""){
		$ID = `getNodeNamespace $FKjoint[0]`;
	}
	string $parentJoint[];
	string $pnode[]= `listRelatives -p -type "joint" $FKjoint[0]`;
	$parentJoint[0] = $pnode[0];
	print("parentJoint " +$parentJoint[0] + "\n");
	for ($nn=1; $nn<size($FKjoint);$nn++){
		$parentJoint[$p]=  $FKjoint[$nn-1];
		print("parentJoint " +$parentJoint[$p] + "\n");
		$p++;
	}

	//バインドジョイントからメッシュオブジェを探し全頂点を取得
	string $bodymesh = $ID+":MODEL";
	if(!`objExists $bodymesh`){
		string $scenemesh[] = `ls -ni -type "mesh"`;
		$bodymesh = $scenemesh[0];
	
		string $obj[] =`ls -ni -dag -type "mesh" $bodymesh ($ID+":NOTOUTPUT")`;
		for($shape in $obj){
			string $p[] =`listRelatives -p $shape`;
				$VerList = stringArrayCatenate ($VerList,(`ls -fl ($p[0] + ".vtx[*]")`));
		}
	}
	for ($n=0; $n<size($FKjoint);$n++){
		string $joint[];
		string $meta = `yjp_rig_getMeta $FKjoint[$n]`;
		if(attributeExists ("source", $meta)){
			$joint = `listConnections -t "joint" ($meta + ".source")`;
		} 
		print("joint " + $joint[0] + "\n");
		
		string $PC[]=`listConnections -t "parentConstraint" ($FKjoint[$n]+".t")`;
		print("PC " + $PC[0] + "\n");
		if (size($PC[0]) < 1)break;
		
		$joint =`listConnections -t "joint" ($PC[0] +".constraintTranslateX")`;
		print("joint " + $joint[0] + "\n");
		if (size($joint[0]) < 1)break;
		if($joint[0] == ($ID + ":Root")){
			float $bbpos[] = `xform -q -os -bb $bodymesh`;
			$Width = (abs($bbpos[0]) + abs($bbpos[2]) +abs($bbpos[3]) + abs($bbpos[5]))*0.5 ;
			print($joint[0] + " root size " + $Width + " \n");
			
			break;
		}
		if(`attributeExists "worldMatrix" $joint[0]`){
			$sclist = `listConnections -type "skinCluster" ($joint[0] +".worldMatrix")`;
			print("skinCluster " + $sclist[0] + "\n");
			if (size($sclist[0]) < 1)break;
		}
	}
	if($sclist[0] !=""){
		string $model[];
		string $shpename[] = `skinCluster -q -g $sclist[0]`;
		$model = `listRelatives -p $shpename[0]`;
		if(`nodeType $shpename[0]` == "mesh"){
			$VerList = `filterExpand -sm 31 ($model[0]+".vtx[*]")`;
		}
		else if (`nodeType $shpename[0]` == "nurbsSurface"){
			$VerList = `filterExpand -sm 28 ($model[0]+".cv[*][*]")`;
		}
	}
	
	//各頂点座標
	vector $xyz[];
	for ($n=0 ; $n < size($VerList) ; $n++){
		float $VertexValueFirst[] = `pointPosition -w $VerList[$n]`;
		$xyz[$n] =  <<$VertexValueFirst[0],$VertexValueFirst[1],$VertexValueFirst[2]>>;
	}

	//各ジョイントにFKを仕込む
	string $addFKtxt = "";
	if(!`gmatch $FKjoint[0] "*_FK"`)$addFKtxt = "_FK";
	string $UpNode = `createNode "transform" -n ($FKjoint[0] + "_upNode")`;
	setAttr "transform1.translateY" 5;
	string $FKjointNext ;

	string $tempNode = ($FKjoint[0] + "_tempNode");
	string $joint_0list[];
	for($nn = 0;$nn<`size$FKjoint`;$nn++){
		int $cns = 0;
		print ("FKctrlCreate > " + $FKjoint[$nn] +"\n");
		$joint_ctrl[$nn] = $FKjoint[$nn] + $addFKtxt + "_ctrl";
		string $FKjoint_SDK = ($FKjoint[$nn] + $addFKtxt + "_SDK");
		string $FKjoint_0 = ($FKjoint[$nn] + $addFKtxt + "_0");

		if(!`objExists $joint_ctrl[$nn]`){
			//最短距離の頂点を探しその距離の1.5倍のコントローラサイズにする
			if(`nodeType $FKjoint[$nn]` == "joint" && $FKjoint[$nn] != ($ID+":Root")){
				float $distanceList[];
				float $jointposA[],$jointposB[];
				
				setAttr ($FKjoint[$nn] + ".segmentScaleCompensate") 0;

				//ジョイントの座標
				$jointposA = `xform -q -a -ws -t $FKjoint[$nn]`;
				vector $A = <<$jointposA[0],$jointposA[1],$jointposA[2]>>;

				string $nextJoint[] = `listRelatives -c -type "joint" $FKjoint[$nn]`;
				if(size($nextJoint)){
					for ($n=0; $n<(size($nextJoint)); $n++){
						$jointposB = `xform -q -a -ws -t $nextJoint[$n]`;
						vector $B = <<$jointposB[0],$jointposB[1],$jointposB[2]>>;
						float $val = abs(mag($A-$B));
						if($Length <$val)$Length = $val;
					}
				}
				else{
					$Length = $Length/2;
				}
				
				for ($n=0 ; $n < size($xyz) ; $n++){
					//ジョイントと頂点の距離
					float $distance = abs(mag($A-$xyz[$n]));
					//距離の最小値
					$distanceList[$n] = $distance;
				}
				
				$distanceList = `sort $distanceList`;
				print("Minimum vertex distance " + $distanceList[0] + "\n");
				int $p;
				int $range ;
				int $vtxnum = size($distanceList);
				print($vtxnum + " vtx count\n");

				$range = ceil($vtxnum/30);
				if($range == 0)$range = 8;
				
				for ($p=0; $p<$range;$p++){
					$Width += $distanceList[$p];
				}
				$Width = $Width/$range;
				//print("Width " + $Width + " p "+ $p + "\n");
				//if($p!=0)$Width = $Width/$p;
				print($Width + "\n");
				
				//小さすぎた場合0.5とする
				if($Width < 0.5)$Width = 0.5;
				if($Width > 3.0)$Width = ($Length*0.3);
			}
			else if($FKjoint[$nn] != ($ID+":Root_FK")){
				print("Size 2.0 when not a joint\n");
				$Width =  2.0;
			}

			//箱型カーブコントローラ
			RigCurveBox $joint_ctrl[$nn] $Width $Length $yjp_RiGctrlColor[1];
			
			//カーブコントローラ
			//RigCurveCircle $joint_ctrl[$nn] $Width $color;
			$Width = 0.5;
			print($FKjoint[$nn]+"\n");
			
			setAttr -keyable false -channelBox false ($joint_ctrl[$nn] + ".v");
			//子のジョイントに向けて回転
			//yjp_RotateTowardChild $FKjoint[$nn] $joint_ctrl[$nn];

			string $mirNode[];

			print("Creating initial value group and SDK group\n");
			
			$joint_0list[$nn] = $FKjoint_0;

			group -w -em -n $FKjoint_SDK;
			group -w -em -n $FKjoint_0;
			
			parent $joint_ctrl[$nn] $FKjoint_SDK;
			parent $FKjoint_SDK $FKjoint_0;

			print($FKjoint[$nn] + "\n");
		
			string $jointM;
			//$jointM = `substitute "_FK" $FKjoint[$nn] "_m"`;
			
			print($jointM + "_Edit\n");
			if($nn == 0){
				setAttr ($FKjoint_0 + ".t") $pos[0] $pos[1] $pos[2];
				setAttr ($FKjoint_0 + ".r") $rot[0] $rot[1] $rot[2];
				$tempNode = `createNode "transform" -n $tempNode`;
				setAttr ($tempNode + ".t") 0 10 0;
				parent -r $tempNode $FKjoint[0];
			}
			$jointM = $FKjoint[$nn] +"_m";
			if(`objExists $jointM`){
				matchTransform $FKjoint_0 $jointM;
			}	
			yjp_rig_connectMeta $metaNode "FKctrl" $joint_ctrl[$nn] "metaParent";
			yjp_rig_connectMeta $metaNode "FKsdk" $FKjoint_SDK "metaParent";
			yjp_rig_connectMeta $metaNode "FKoffset" $FKjoint_0 "metaParent";
			//parent ($FKjoint[$nn] + "_0") $FKjoint[$nn];
			//resetAttrs ($FKjoint[$nn] + "_0");
			//parent -w ($FKjoint[$nn] + "_0");
			//メタノード取得
			print ($FKjoint[$nn]+"\n");
			$metaNode = `yjp_rig_getMeta $FKjoint[$nn]` ;
			print($metaNode + " meta\n");
			//rootMeta
			//yjp_rig_connectMeta $metaNode "metaParent" $ParentMeta "metaChild";
		}
		if($nn > 0){
			//FKのペアレント
			parent $FKjoint_0 ($parentJoint[$nn] + $addFKtxt + "_ctrl");
			string $cildMeta = `yjp_rig_getMeta $FKjoint[$nn]`;
			string $parentMetaNode = `yjp_rig_getMeta $parentJoint[$nn]`;
			yjp_rig_connectMeta $parentMetaNode "metaChild" $cildMeta "metaParent";
			print ("parent > " + $FKjoint[$nn] + $addFKtxt+"_0 > " + $parentJoint[$nn] + $addFKtxt+"_ctrl" +"\n");
		}

		//connectAttr ($UtilityName + "_swiches_if.outColorR") ($joint_ctrl[$nn] + ".visibility");
	}
	vector $direction = `xform -q -ws -t $FKjoint[0]`;
	int $mirror = 1;
	if($symmetry == 1 && $direction.x < 0){
		$mirror = -1;
	}
	yjp_rig_snapChains $FKjoint $joint_0list $tempNode $mirror;
	delete $tempNode;
	for($nn = 0;$nn<`size$FKjoint`;$nn++){
		print(($joint_ctrl[$nn]+"_PC") +" "+$joint_ctrl[$nn] +" "+$FKjoint[$nn] + "\n");
		string $pconst[] = `parentConstraint -mo -n ($joint_ctrl[$nn]+"_PC") $joint_ctrl[$nn] $FKjoint[$nn]`;
		string $sconst[] = `scaleConstraint -mo -n ($joint_ctrl[$nn]+"_SC") $joint_ctrl[$nn] $FKjoint[$nn]`;
		yjp_rig_connectMeta $metaNode "Utility" $pconst[0] "metaParent";
		yjp_rig_connectMeta $metaNode "Utility" $sconst[0] "metaParent";	
	}
	//"Controls"グループにペアレント
	//print ($FKjoint[0] + "_0"+ "\n") ;
	string $parentnode[] = `listRelatives -p ($FKjoint[0] + $addFKtxt + "_0")`;
	if(`objExists ($ID + ":Controls")`){
		if($parentnode[0] != ($ID + ":Controls")){
			parent ($FKjoint[0] + $addFKtxt+"_0") ($ID + ":Controls");
		}
	}
	//connectAttr -f ($ID + "Ground_FK_ctrl.scale") ($FKjoint[0] + "_0.scale");
	
	print ("yjp_rig_makeFKaxis END> " + $joint_ctrl[0] + "\n");
	return $joint_ctrl;
}